/// INSTRUCTIONS
instruction = @{ ("print" | "graph" | "out") ~ WHITE_SPACE }
as_keyword = { "as" }
// can not be i because i represent imaginary number
variable_name = { !instruction ~ !as_keyword ~ !data_type ~ !"i" ~ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_" | "-" )* }

/// TYPES
data_type = { "decimal" | "idecimal" | "complex" | "frac" | "ifrac" | "irrational" }
file_path = { ASCII_ALPHANUMERIC ~ (!"." ~ !"*" ~ !"\"" ~ !"<" ~ !">" ~ !"|" ~ ASCII)* ~ "." ~ ASCII_ALPHANUMERIC+ }
boolean = { "true" | "false" }

/// OPERATORS
multiplication = { "*" }
division = { "/" }
power = { "^" }
modulo = { "%" }
addition = { "+" }
subtraction = { "-" }
open_parentheses = { "(" }
close_parentheses = { ")" }
operators = _{ open_parentheses | close_parentheses | power | modulo | multiplication | division | addition | subtraction }

assignment_operator = { "=" }
comma = { "," }

/// NUMBERS
positive_whole_numbers = _{ ASCII_DIGIT+ }
negative_whole_numbers = _{ subtraction ~ positive_whole_numbers }

fraction = @{ positive_whole_numbers ~ "/" ~ positive_whole_numbers }
decimal = @{ positive_whole_numbers ~ "." ~ positive_whole_numbers }
numeric_expression = _{ decimal | fraction | negative_whole_numbers | positive_whole_numbers }

/// EXPRESSIONS
simple_math = _{ (numeric_expression ~ variable_name?) | variable_name }
math_expression = !{ simple_math ~ (operators ~ simple_math)* }
function_call_expression = !{ variable_name ~ open_parentheses ~ math_expression ~ (comma ~ math_expression)* ~ close_parentheses }

expression = ${ (function_call_expression | math_expression) ~ (WHITE_SPACE? ~ as_keyword ~ WHITE_SPACE ~ data_type)? }

/// STATEMENTS
operation_argument = ${ variable_name ~ WHITE_SPACE ~ (boolean | file_path | data_type | expression) }
operation = { instruction ~ expression }
in_operation = ${ "in" ~ WHITE_SPACE ~ variable_name }
use_operation = ${ "use" ~ WHITE_SPACE ~ file_path }

constant = { variable_name ~ (comma ~ variable_name)* ~ assignment_operator ~ expression }
function_declaration = { variable_name ~ open_parentheses ~ variable_name ~ (comma ~ variable_name)* ~ close_parentheses ~ assignment_operator ~ expression }
line_comment = _{ "//" ~ ANY* } 

line = { line_comment | in_operation | use_operation | operation | operation_argument | function_declaration | constant }
file = { SOI ~ "\n"* ~ (line ~ "\n"+)* ~ line? ~ EOI }

/// SPECIAL
WHITESPACE = _{ " " }
COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }